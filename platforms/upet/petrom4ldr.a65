
/*
 * this file installs the usbrom driver in bank 7 and patches the BASIC 4 ROM
 * to include the ROM usb companion functions.
 *
 * Therefore, at the end of this file, a page of companion code is expected that is
 * installed into $dfxx in BASIC ROM.
 * After that, up to 16k of USB driver code and data are expected, that are copied
 * to $1000 in bank 7 (where the USB driver code resides)
 */

COMPADDR	=$df00
INSTADDR	=$9000		; needs to be in upper 32k as it remaps lower 32k
;DRIVERADDR	=$079000	; bank 15 = $078000 + $1000 DRIVERADDR
DRIVERADDR	=$1000		; bank 15 = $078000 + $1000 DRIVERADDR

MAP_USB		=15
MAP_BAS		=0
MAPREG		=$e802

lock		=$0700ff
VCOUNT		=$99

driver_poll	=DRIVERADDR+3
driver_init	=DRIVERADDR+0

driver_pause_a	=DRIVERADDR+6+1
driver_print_a	=DRIVERADDR+9+1

	.word $0401
	*=$0401

	.word eol		; BASIC link pointer
	.word 10		; line number
	.byt $9e, "1040", 0	; BASIC code (tokenized)
eol	.word 0			; BASIC link pointer, 0 means end of code
	.dsb 1040-*

	; start after sys
	
	; make ROM writable
	lda #%01100000
	sta $e801

	; copy petromcomp over ROM at page $dfxx
	ldy #0
pc1	lda petrom_comp,y
	sta COMPADDR,y
	iny
	cpy #usbrom - petrom_comp
	bne pc1

	; copy petrom_install over ROM at page $9000
	ldy #0
pc1a	lda petrom_install,y
	sta INSTADDR,y
	iny
	cpy #petrom_comp - petrom_install
	bne pc1a

	; write-protect ROM again
	lda #%11100000
	sta $e801	

	sei

	; native mode
	clc
	xce
	; 16bit index registers
	rep #%00010000
	.xl

	ldx #0
pc2	lda usbrom,x
	sta DRIVERADDR+(MAP_USB << 15),x
	inx
	cpx #end - usbrom
	bne pc2

	; 8bit index registers
	sep #%00010000
	.xs
	; emulation mode
	sec
	xce

	cli
inc $8000
	jsr usb_init

	; unfortunately just calling $df00 directly 
	; makes the USB keyboard break when pressing ENTER
	
	ldy #0
tl	lda text,y
	beq tend
	jsr $ffd2
	iny
	bne tl
tend	rts
text
	.asc 13,"USB INSTALLED",13, 0	

petrom_install
	*=INSTADDR
#include petrom4_install.a65
	*=petrom_install+*-INSTADDR
petrom_comp
	*=COMPADDR
#include petrom4_comp.a65
	*=petrom_comp+*-COMPADDR
usbrom
	.bin 0,0, "petrom"
end
